<html>
  <head>
    <title>W06: Task 02</title>
  </head>
  <body>
    <script src="https://threejs.org/build/three.min.js"></script>
    <h1>Compare the rendering result</h1>
    top left :Gouraud shading - Lambertian reflection model<br>
    top right :Gouraud shading - Phong reflection model<br>
    bottom left :Phong shading - Lambertian reflection model<br>
    bottom right :Phong shading - Phong reflection model<br>
    <script type="x-shader/x-vertex" id="shader.vert">
      varying vec3 point_position;
      varying vec3 normal_vector;
      varying vec3 point_color;
      void main()
      {
          point_position = (modelViewMatrix * vec4(position, 1.0)).xyz;
          normal_vector = normalMatrix * normal;
          point_color = color;
          gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
      }
    </script>
    
    <script type="x-shader/x-vertex" id="shader.vert.Lambertian">
      varying vec3 point_color;
      uniform vec3 light_position;      
      vec3 LambertianReflection(vec3 C, vec3 L, vec3 N)
      {
          float ka = 0.4;
          float kd = 0.6;
          float dd = max(dot(N,L), 0.0);
          float Ia = ka;
          float Id = kd * dd;
          return C * (Ia + Id);
      }
      void main()
      {
          vec4 point_position = modelViewMatrix * vec4(position, 1.0);
          vec3 normal_vector = normalMatrix * normal;   
          vec3 C = color;
          vec3 L = normalize(light_position - point_position.xyz);
          vec3 N = normalize(normal_vector);
          point_color = LambertianReflection(C, L, N);
          gl_Position = projectionMatrix * point_position;
      }
    </script>
    
    <script type="x-shader/x-vertex" id="shader.vert.Phong">
      varying vec3 point_color;
      uniform vec3 light_position;
      uniform vec3 camera_position;
      vec3 PhongReflection(vec3 C, vec3 L, vec3 N, vec3 V)
      {
          float ka = 0.3;
          float kd = 0.5;
          float ks = 0.8;
          float n = 50.0;
          vec3 R = reflect(-L, N);
          float dd = max(dot(N,L), 0.0);
          float ds = pow(max(dot(R,V), 0.0), n);
          if(dd <= 0.0){ds = 0.0;}
          float Ia = ka;
          float Id = kd * dd;
          float Is = ks * ds;
          return C * (Ia + Id + Is);
      }
      
      void main()
      {
          vec4 point_position = modelViewMatrix * vec4(position, 1.0);
          vec3 normal_vector = normalMatrix * normal;
          vec3 C = color;
          vec3 L = normalize(light_position - point_position.xyz);
          vec3 N = normalize(normal_vector);
          vec3 V = normalize(camera_position - point_position.xyz);
          point_color = PhongReflection(C, L, N, V);
          gl_Position = projectionMatrix * point_position;
      }
    </script>
    
    <script type="x-shader/x-fragment" id="shader.frag">
      varying vec3 point_color;
      void main()
      {
          gl_FragColor = vec4( point_color, 1.0 );
      }
    </script>

    <script type="x-shader/x-fragment" id="shader.frag.Lambertian">
      varying vec3 point_position;
      varying vec3 normal_vector;
      varying vec3 point_color;
      uniform vec3 light_position;
      vec3 LambertianReflection(vec3 C, vec3 L, vec3 N)
      {
	  float ka = 0.4;
	  float kd = 0.6;
	  float dd = max(dot(N,L), 0.0);
	  float Ia = ka;
	  float Id = kd * dd;
	  return C * (Ia + Id);
      }
      void main()
      {
	  vec3 C = point_color;
	  vec3 L = normalize(light_position - point_position);
	  vec3 N = normalize(normal_vector);
	  vec3 color_result = LambertianReflection(C, L, N);
	  gl_FragColor = vec4( color_result, 1.0 );
      }
    </script>
    
    <script type="x-shader/x-fragment" id="shader.frag.Phong">
      varying vec3 point_position;
      varying vec3 normal_vector;
      varying vec3 point_color;
      uniform vec3 light_position;
      uniform vec3 camera_position;
      vec3 PhongReflection(vec3 C, vec3 L, vec3 N, vec3 V)
      {
          float ka = 0.3;
	  float kd = 0.5;
	  float ks = 0.8;
	  float n = 50.0;
	  vec3 R = reflect(-L, N);
	  float dd = max(dot(N,L), 0.0);
	  float ds = pow(max(dot(R,V), 0.0), n);
	  if(dd <= 0.0){ds = 0.0;}
	  float Ia = ka;
	  float Id = kd * dd;
	  float Is = ks * ds;
	  return C * (Ia + Id + Is);
      }
      void main()
      {
	  vec3 C = point_color;
	  vec3 L = normalize(light_position - point_position);
	  vec3 N = normalize(normal_vector);
	  vec3 V = normalize(camera_position - point_position);
	  vec3 color_result = PhongReflection(C, L, N, V);
	  gl_FragColor = vec4( color_result, 1.0 );
      }
    </script>
    
    <script src="main_t2-2.js"></script>
    <script>
      main();
    </script>
  </body>
</html>
